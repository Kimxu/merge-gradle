////////////////////////////////////////////
//////////// MAVEN PUBLISHING //////////////
////////////////////////////////////////////
apply plugin: 'maven-publish'

publishing {
    publications {
        androidLibs(MavenPublication) {
            groupId = GROUP
            artifactId POM_ARTIFACT_ID
            version = VERSION_NAME

            artifact "${project.buildDir}/outputs/aar/${getArtifactFileName()}" //bundleRelease

            List<String> embedList = new ArrayList<>();
            Map<String, ResolvedDependency> depList = new LinkedHashMap<>();

            //List all embedded dependencies
            configurations.embedded.allDependencies.each {
                def depName = String.format("%s:%s", it.group, it.name)
                embedList.add(depName);
            }

            //Collect all first level dependencies except embedded ones
            configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each {
                ResolvedDependency dep ->
                    def depName = String.format("%s:%s", dep.moduleGroup, dep.moduleName)
                    if (!embedList.contains(depName) && !depList.containsKey(depName)) {
                        depList.put(depName, dep)
                    }
            }

            //Collect all second level dependencies of embedded ones
            configurations.embedded.resolvedConfiguration.firstLevelModuleDependencies.each {
                //Second Level Depenencies
                it.children.each {
                    ResolvedDependency dep ->
                        def depName = String.format("%s:%s", dep.moduleGroup, dep.moduleName)
                        if (!embedList.contains(depName) && !depList.containsKey(depName)) {
                            depList.put(depName, dep)
                        }
                }
            }

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies, adding a <dependency> node for each
                depList.values().each {
                    ResolvedDependency dep ->
                        def hasGroup = dep.moduleGroup != null
                        def hasName = (dep.moduleName != null && !"unspecified".equals(dep.moduleName) && !"".equals(dep.moduleVersion))
                        def hasVersion = (dep.moduleVersion != null && !"".equals(dep.moduleVersion) && !"unspecified".equals(dep.moduleVersion))

                        if (hasGroup && hasName && hasVersion) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dep.moduleGroup)
                            dependencyNode.appendNode('artifactId', dep.moduleName)
                            dependencyNode.appendNode('version', dep.moduleVersion)
                        }
                }
            }
        }
    }
    repositories {
        maven {
            url "http://127.0.0.1:8081/nexus/content/repositories/releases"
            credentials {
                username "admin"
                password "haoadmin"
            }
        }
    }
}
/*
artifactory {
    contextUrl = REPOSITORY_URL
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = VERSION_NAME.contains("SNAPSHOT") ? SNAPSHOT_REPOSITORY_KEY : RELEASE_REPOSITORY_KEY
            username = ARTIFACTORY_USERNAME
            password = ARTIFACTORY_PASSWORD
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['build.status': "$it.project.status".toString()]
            publishPom = true
            publishIvy = false
        }
    }
}*/

